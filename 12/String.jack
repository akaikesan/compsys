class String{


	field int lng;
	field int max;
	field Array body;


	constructor String new(int maxLength){
		if(maxLength = 0){let maxLength = 1;}
		let max = maxLength;
		let lng  = 0;
		let body = Array.new(maxLength );

		return this;
	}


	method void dispose(){
		do Memory.deAlloc(this);
		return;
	}

	method int length(){
		return lng;
	}
	
	method char charAt(int j){
		if(~(j < max)){
			do Sys.error(15); 
		}else{
			return body[j];
		}
		return 0;
	}

	method void setCharAt(int j, char c){
		if(j > max){
			do Sys.error(16);
		}else{
			let body[j] = c;
		}
		return;
	}

	method String appendChar(char c){
		let body[lng] = c;
		let lng = lng + 1;
		return this;
		
	}

	method void eraseLastChar(){
		let body[lng] = 0;
		let lng = lng - 1;
		return;
	}


	method int intValue (){

		var int cnt;		//cnt counts while loop. 
		var int ans;		//this method return variable named "ans"
		var int curdigit; 	//the digit each while loop refers.

		let cnt = 0;
		let ans = 0;	

		
		if(body[0] = 45){let cnt = cnt + 1;}

		while(cnt < lng){
				
			if((47 < body[cnt]) & (body[cnt] < 58)){
				let curdigit = body[cnt] - 48;
			}else{
				return 0;
			}
			let ans = ans * 10 + curdigit;
			let cnt = cnt + 1;	
		}

		if(body[0] = 45){let ans = -ans;}


		return ans;
	}


	method void setInt(int i){

		var int abs_i;

		let lng = 0;
		
		let abs_i = Math.abs(i);	

		if(~(i = abs_i)){ do appendChar(45); }	

		do int2String(abs_i);

		return;
		
	}

	method void int2String(int i){

		var int firstdgt;
		
		let firstdgt = i - ((i/10) * 10);

		if(i = 0){ return; }
		
		else{ do int2String(i/10); }
		
		do appendChar(firstdgt + 48);

		return;
	}



	function char backSpace(){
		return 129;
	}

	function char newLine(){
		return 128;
	}
	
	function char doubleQuote(){
		return 34;
	}
}
